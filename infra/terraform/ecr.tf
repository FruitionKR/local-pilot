# 서비스별 컨테이너 레지스트리. pipeline-api·ingest-worker는 같은 이미지(fruition-pipeline)를 쓴다.
locals {
  ecr_repos = ["document-svc", "access-svc", "pipeline", "converter"]
}

resource "aws_ecr_repository" "services" {
  for_each = toset(local.ecr_repos)

  name = "${var.project}-${each.key}"

  image_scanning_configuration {
    scan_on_push = true
  }
}

# 최근 10개 이미지만 보존
resource "aws_ecr_lifecycle_policy" "services" {
  for_each   = aws_ecr_repository.services
  repository = each.value.name

  policy = jsonencode({
    rules = [
      {
        rulePriority = 1
        description  = "keep last 10 images"
        selection = {
          tagStatus   = "any"
          countType   = "imageCountMoreThan"
          countNumber = 10
        }
        action = { type = "expire" }
      }
    ]
  })
}
